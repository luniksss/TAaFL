# Грамматика выражений языка МЯУ#
## Синтаксис выражений
Выражения могут содержать:
- литералы чисел(целые(`цыфорка`) и вещественные(`рилцыфорка`))
- литералы строк(`нитка`)
- логические литералы(`кринж, !кринж`)
- арифметические операторы (бинарные)
- операторы сравнения
- логические операторы
- константы
- круглые скобки для группировки
- вызовы функций

## Операторы
Арифметические операторы:  
| символы | операция                 | ассоциативность |
| ------- | ------------------------ | --------------- |
| `+`     | сложение или унарный "+" | левая |
| `-`     | вычитание или унарный "-" | левая |
| `*`     | умножение | левая |
| `/`     | деление нацело | левая |
| `%`     | остаток от деления | левая |
| `^`     | возведение в степень | правая |  

Операторы сравнения:  
| символы | операция                 |
| ------- | ------------------------ |
| `>`     | строгое больше |
| `>=`     | нестрогое больше |
| `<`     | строгое меньше |
| `<=`     | нестрогое меньше |
| `==`     | равенство |
| `!=`     | неравенство |  

Логические операторы:  
| символы | операция                 | ассоциативность |
| ------- | ------------------------ | --------------- |
| `&&`     | логическое И | левая |
| `\|\|`     | логическое ИЛИ | левая |

## Приоритет операторов
| Приоритет (по убыванию) | Операторы     |
| ----------------------- | ------------- |
| 1                       | `+`, `-` (унарные)   |
| 2                       | `^`           |
| 3                       | `*`, `/`, `%` |
| 4                       | `+`, `-`      |
| 5                       | `>`, `>=`, `<`, `<=` |
| 6                       | `==`, `!=`      |
| 7                       | `&&`      |
| 8                       | `\|\|`      |

## Константы
| константа | обозначение                 | значение |
| ------- | ------------------------ | --------------- |
| `π`     | пи | 3.141592653589793 |
| `e`     | эклер | 2.718281828459045 |

## Грамматика в нотации EBNF
Базовые символы
```
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

letter = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" 
       | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" 
       | "u" | "v" | "w" | "x" | "y" | "z" 
       | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" 
       | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" 
       | "U" | "V" | "W" | "X" | "Y" | "Z" ;
```  
Литералы и базовые элементы  
```
literal = number | string ;
number = integer | real ;
integer = digit, { digit } ;
real = digit, { digit }, ".", digit, { digit } ;
string = '"', { anyChar - '"' | escapeSequence }, '"' ;
anyChar = ? любой символ Unicode ? ;
escapeSequence = "\\", ( "\"" | "\\" | "n" | "t" ) ;
boolean = "кринж" | "!кринж" ;
constant = "пи" | "эклер" ;
identifier = ( letter | "_" ), { letter | digit | "_" } ;
functionCall = "лапка", identifier, "(", [ expression, { ",", expression } ], ")" ;
```  
Основные конструкции  
```
expression = logicalOr ;
logicalOr = logicalAnd, { "||", logicalAnd } ;
logicalAnd = comparison, { "&&", comparison } ;
comparison = additive, [ ( ">" | ">=" | "<" | "<=" | "==" | "!=" ), additive ] ;
additive = multiplicative, { ( "+" | "-" ), multiplicative } ;
multiplicative = power, { ( "*" | "/" | "%" ), power } ;
power = unary, [ "^", power ] ;
unary = [ "+" | "-" ], primary ;
primary = literal | constant | boolean | identifier | "(", expression, ")" | functionCall ;
```
